<!-- For the next release
---
position: 8
title: Anotated Source
---
-->

    """
    Alchemy.js is a graph drawing application for the web.
    Copyright (C) 2014  GraphAlchemist, Inc.

    This program is free software: you can redistribute it and/or modify
    it under the terms of the GNU Affero General Public License as published by
    the Free Software Foundation, either version 3 of the License, or
    (at your option) any later version.

    This program is distributed in the hope that it will be useful,
    but WITHOUT ANY WARRANTY; without even the implied warranty of
    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
    GNU Affero General Public License for more details.

    You should have received a copy of the GNU Affero General Public License
    along with this program.  If not, see <http://www.gnu.org/licenses/>.
    lets
    """

    class Alchemy
        constructor: (userConf=null) ->
            @a = @

            @version  = "#VERSION#"

            # Alchemy API methods
            @api    = new API @
            @get    = @api.get
            @create = @api.create
            @set    = @api.set
            @filter = @api.filter
            @forceLayout = @api.force

            @drawing  =
                DrawEdge : DrawEdge   @
                DrawEdges: DrawEdges  @
                DrawNode : DrawNode   @
                DrawNodes: DrawNodes  @
                NodeUtils: @NodeUtils @
            
            @controlDash = @controlDash @
            @stats = @stats @
            @search = @search @

            @layout     = Layout
            @clustering = Clustering

            @models =
                Node: @Node @
                Edge: @Edge @

            @utils        = warnings: new warnings @
            @filters      = @filters @
            @exports      = @exports @
            @visControls  = {}
            @styles       = {}
            @editor       = {}
            @log          = {}

            @state =
                "interactions": "default"
                "layout": "default"

            @startGraph     = @startGraph @
            @updateGraph    = @updateGraph @
            @generateLayout = @generateLayout @
            @svgStyles      = @svgStyles @
            @interactions   = @interactions @
            @plugins        = @plugins @

	        # alchemy._nodes stores a node object as the value with the unique
            # id specified in the GraphJSON.
            @_nodes = {}

            # alchemy._edges stores an array of edges under every unique id.
            # the edge model is a key value pair where the "key" is either
            # 1) an id provided in the GraphJSON data, or 2) an id generated by
            # Alchemy taking the format of "#{source}-#{target}".
            # The value is an array of edge 'packets', where the length of the array
            # is typically 1.
            @_edges = {}

            # Bind legacy API methods to earlier location
            # These will be deprecated on release-1.0
            @getNodes = @get.getNodes
            @getEdges = @get.getEdges
            @allNodes = @get.allNodes
            @allEdges = @get.allEdges

            @begin userConf if userConf

        begin: (userConf) ->
            # overide configuration with user inputs
            conf = @setConf userConf
            Alchemy::instances.push @
            switch typeof @conf.dataSource
                when 'string' then d3.json @a.conf.dataSource, @a.startGraph
                when 'object' then @a.startGraph @a.conf.dataSource
            
            @plugins.init()

            @

        setConf: (userConf) ->
            # apply base themes
            if userConf.theme?
                userConf = _.merge _.cloneDeep(@defaults), @a.themes["#{userConf.theme}"]

            for key, val of userConf
                switch key
                    when "clusterColors"   then userConf["clusterColours"]   = val
                    when "backgroundColor" then userConf["backgroundColour"] = val
                    when "nodeColor"       then userConf[nodeColour]         = val

            @a.conf = _.merge _.cloneDeep(@defaults), userConf

        # All alchemy instances in order of creation.
        instances: []

        getInst: (svg)->
            instNumber = parseInt d3.select(svg).attr("alchInst")
            Alchemy::instances[instNumber]

    root = exports ? this
    root.Alchemy = Alchemy

    #Backwards compatible alchemy.begin() for single instances
    root.alchemy = begin: (config)-> root.alchemy = new Alchemy(config)
